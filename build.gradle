plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
}

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    testCompile group: "junit", name: "junit", version: "4.12"
    testCompile group: "org.reflections", name: "reflections", version: "0.9.10"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

group PACKAGE_GROUP
version PACKAGE_VERSION

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        ArelPublication(MavenPublication) {
            groupId PACKAGE_GROUP
            artifactId PACKAGE_NAME
            version PACKAGE_VERSION

            from components.java

            artifact(javadocJar) {
                classifier = "javadoc"
            }

            artifact(sourceJar) {
                classifier = "sources"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name PACKAGE_NAME
                    description PACKAGE_DESCRIPTION
                    url PACKAGE_WEBSITE
                    scm {
                        url GITHUB_URL
                        connection GITHUB_REPOSITORY_URL
                        developerConnection GITHUB_REPOSITORY_DEVELOPER_URL
                    }
                    licenses {
                        license {
                            name "The MIT License (MIT)"
                            url "https://opensource.org/licenses/MIT"
                            distribution "repo"
                        }
                    }
                    developers {
                        developer {
                            id DEVELOPER_1_ID
                            name DEVELOPER_1_NAME
                            email DEVELOPER_1_EMAIL
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "file:/${project.projectDir}/build/artifacts"
        }
    }
}

bintray {
    user = project.hasProperty("BINTRAY_USER") ? project.property("BINTRAY_USER") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("BINTRAY_API_KEY") ? project.property("BINTRAY_API_KEY") : System.getenv("BINTRAY_API_KEY")

    publish = true

    pkg {
        userOrg = BINTRAY_ORGANIZATION
        repo = BINTRAY_REPOSITORY
        name = BINTRAY_NAME
        desc = PACKAGE_DESCRIPTION
        websiteUrl = PACKAGE_WEBSITE
        issueTrackerUrl = GITHUB_ISSUES_URL
        vcsUrl = GITHUB_REPOSITORY_URL
        licenses = ["MIT"]
        publicDownloadNumbers = true

        githubRepo = GITHUB_REPOSITORY
        githubReleaseNotesFile = GITHUB_README_FILE

        version {
            name = PACKAGE_VERSION
            desc = PACKAGE_DESCRIPTION
            released = new Date()
            vcsTag = "v" + PACKAGE_VERSION

            mavenCentralSync {
                sync = SYNC_MAVEN_CENTRAL
                user = project.hasProperty("SONATYPE_OSS_USER") ? project.property("SONATYPE_OSS_USER") : System.getenv("SONATYPE_OSS_USER")
                password = project.hasProperty("SONATYPE_OSS_PASSWORD") ? project.property("SONATYPE_OSS_PASSWORD") : System.getenv("SONATYPE_OSS_PASSWORD")
                close = "1"
            }
        }
    }

    publications = ["ArelPublication"]
}
