plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.6"
}

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    testCompile group: "junit", name: "junit", version: "4.12"
    testCompile group: "org.reflections", name: "reflections", version: "0.9.10"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

group GROUP
version VERSION

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        ArelPublication(MavenPublication) {
            from components.java

            artifact(javadocJar) {
                classifier = "javadoc"
            }

            artifact(sourceJar) {
                classifier = "sources"
            }
        }
    }

    repositories {
        maven {
            url "file:/${project.projectDir}/build/artifacts"
        }
    }
}

bintray {
    user = project.hasProperty("BINTRAY_USER") ? project.property("BINTRAY_USER") : System.getenv("BINTRAY_USER")
    key = project.hasProperty("BINTRAY_API_KEY") ? project.property("BINTRAY_API_KEY") : System.getenv("BINTRAY_API_KEY")

    pkg {
        repo = "ark"
        name = "arel"
        userOrg = "arauk"
        licenses = ["MIT"]
        vcsUrl = "https://github.com/arauk/ark-arel.git"

        version {
            name = VERSION
            desc = DESCRIPTION
            released = new Date()
            vcsTag = "v" + VERSION
        }
    }

    publications = ["ArelPublication"]
}
